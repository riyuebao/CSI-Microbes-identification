from os.path import join
import pandas as pd
from snakemake.utils import min_version
##### set minimum snakemake version #####
min_version("5.1.2")

ruleorder: download_FQ1_files_comb > download_FQ1_files
ruleorder: download_FQ2_files_comb > download_FQ2_files
# batch = config["batch"]
##### load config and sample sheets #####
configfile: "config/PathSeq-config.yaml"
#validate(config, schema="schemas/config.schema.yaml")
wildcard_constraints:
    patient="MEEI[0-9]{1,2}"

patients = pd.read_csv(config["patients"], sep="\t").set_index("patient", drop=False)
# validate(samples, schema="schemas/samples.schema.yaml")
samples = pd.read_csv(config["samples"], dtype=str, sep="\t").set_index(["patient", "sample"], drop=False).sort_index()
# samples = samples.groupby("batch").sample(1, random_state=0)
# samples = samples.loc[samples["batch"] == "HNSCC6_P15_LN"]
valid_batches = [
    "HN26_P13", "HNSCC28_P12", "HNSCC25_P22", "HN26_P6", "HNSCC25_P19_LN",
    "HNSCC18_P4", "HNSCC26_P3", "HNSCC17_P7", "HNSCC24_P3", "HNSCC25_P2",
    "HN26_P14", "HNSCC22_P5", "HNSCC25_P3", "HNSCC18_P2", "HNSCC25_P1_pri",
    "HN28_P5", "HNSCC17_P4", "HN28_P14", "HN25_P5", "HN25_P24", "HNSCC28_P13",
    "HNSCC26_P1_pri", "HN25_P25", "HNSCC26_P23", "HN26_P26", "HNSCC26_P24",
    "HNSCC22_P4", "HNSCC28_P1", "HN26_P5", "HNSCC17_P2", "HN28_P15", "HNSCC17_P3",
    "HNSCC26_P11_R_LN", "HNSCC25_P4", "HN28_P3P4", "HNSCC18_P7", "HNSCC28_P2",
    "HNSCC26_P12", "HN25_P23", "HNSCC18_P3", "HNSCC20_P5_pri", "HN26_P25"
]
# Directories
FASTQ_DIR = join("FASTQ", "raw")

# Temporary Files
FQ1_L1_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L001_R1.fastq.gz")
FQ1_L2_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L002_R1.fastq.gz")
FQ1_L3_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L003_R1.fastq.gz")
FQ1_L4_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L004_R1.fastq.gz")
FQ2_L1_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L001_R2.fastq.gz")
FQ2_L2_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L002_R2.fastq.gz")
FQ2_L3_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L003_R2.fastq.gz")
FQ2_L4_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L004_R2.fastq.gz")

# Intermediate Files
FASTQ1_FILE = join(FASTQ_DIR,  "{patient}-{sample}_1.fastq.gz")
FASTQ2_FILE = join(FASTQ_DIR,  "{patient}-{sample}_2.fastq.gz")
TRIMMED_FASTQ1_FILE = join("FASTQ", "trimmed", "{patient}-{sample}_1.fastq.gz")
TRIMMED_FASTQ2_FILE = join("FASTQ", "trimmed", "{patient}-{sample}_2.fastq.gz")
TRIMMED_UNPAIRED_FILE = join("FASTQ", "trimmed", "{patient}-{sample}_3.fastq.gz")

# Output Files
PATHSEQ_BAM = join("output", "PathSeq", "{patient}-{sample}", "pathseq.bam")

include: "../RNA-snakemake-rules/rules/genome.smk"
include: "../RNA-snakemake-rules/rules/trim.smk"
include: "../pathogen-discovery-rules/rules/filter-host-reads.smk"
include: "../pathogen-discovery-rules/rules/PathSeq-single.smk"


rule all:
    input:
        expand(config["PathSeq"]["bam_file"], zip, patient=samples["patient"], sample=samples["sample"])


rule download_FQ1_files:
    group:
        "FASTQ"
    params:
        fq = lambda wildcards: samples.loc[(wildcards.patient, wildcards.sample), "fastq_prefix"],
    output:
        fq1=FASTQ1_FILE
    shell:
        #"module load google-cloud-sdk && "
        "gsutil cp {params.fq}_R1.fastq.gz {output.fq1}"

rule download_FQ2_files:
    group:
        "FASTQ"
    params:
        fq = lambda wildcards: samples.loc[(wildcards.patient, wildcards.sample), "fastq_prefix"],
    output:
        fq2=FASTQ2_FILE
    shell:
        "module load google-cloud-sdk && "
        "gsutil cp {params.fq}_R2.fastq.gz {output.fq2}"

# each paired-end sample actually consists of 8 files
# {sample}-{well}_{sample_number}_{lane}_{end}_001.fastq.gz
# we need to download all four fastq files per end and combine them
rule download_FQ1_files_comb:
    group:
        "FASTQ"
    wildcard_constraints:
        sample=".+_comb"
    params:
        fq = lambda wildcards: samples.loc[(wildcards.patient, wildcards.sample), "fastq_prefix"],
    output:
        L1=temp(FQ1_L1_FILE),
        L2=temp(FQ1_L2_FILE),
        L3=temp(FQ1_L3_FILE),
        L4=temp(FQ1_L4_FILE),
        fq1=FASTQ1_FILE
    shell:
        #"module load google-cloud-sdk && "
        "gsutil cp {params.fq}_L001_R1_001.fastq.gz {output.L1}  && "
        "gsutil cp {params.fq}_L002_R1_001.fastq.gz {output.L2} && "
        "gsutil cp {params.fq}_L003_R1_001.fastq.gz {output.L3} && "
        "gsutil cp {params.fq}_L004_R1_001.fastq.gz {output.L4} && "
        "cat {output.L1} {output.L2} {output.L3} {output.L4} > {output.fq1}"

rule download_FQ2_files_comb:
    group:
        "FASTQ"
    wildcard_constraints:
        sample=".+_comb"
    params:
        fq = lambda wildcards: samples.loc[(wildcards.patient, wildcards.sample), "fastq_prefix"],
    output:
        L1=temp(FQ2_L1_FILE),
        L2=temp(FQ2_L2_FILE),
        L3=temp(FQ2_L3_FILE),
        L4=temp(FQ2_L4_FILE),
        fq2=FASTQ2_FILE
    shell:
        "module load google-cloud-sdk && "
        "gsutil cp {params.fq}_L001_R2_001.fastq.gz {output.L1}  && "
        "gsutil cp {params.fq}_L002_R2_001.fastq.gz {output.L2} && "
        "gsutil cp {params.fq}_L003_R2_001.fastq.gz {output.L3} && "
        "gsutil cp {params.fq}_L004_R2_001.fastq.gz {output.L4} && "
        "cat {output.L1} {output.L2} {output.L3} {output.L4} > {output.fq2}"
