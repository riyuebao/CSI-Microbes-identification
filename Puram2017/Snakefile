from os.path import join
import pandas as pd
from snakemake.utils import min_version
##### set minimum snakemake version #####
min_version("5.1.2")
batch = config["batch"]
##### load config and sample sheets #####
configfile: "config/PathSeq-config.yaml"
#validate(config, schema="schemas/config.schema.yaml")
wildcard_constraints:
    patient="MEEI[1-2][0-9]"

patients = pd.read_csv(config["patients"], sep="\t").set_index("patient", drop=False)
# validate(samples, schema="schemas/samples.schema.yaml")
samples = pd.read_csv(config["samples"], dtype=str, sep="\t").set_index(["patient", "sample"], drop=False).sort_index()

# this sample has something weird with pri
samples = samples.loc[~samples["batch"].isin(["HNSCC22_P2"])]
samples = samples.loc[samples["batch"] == batch]
#print(samples)
FASTQ_DIR = join("FASTQ", "raw")

# Intermediate Files
FASTQ1_FILE = join(FASTQ_DIR,  "{patient}-{sample}_R1.fastq.gz")
FASTQ2_FILE = join(FASTQ_DIR,  "{patient}-{sample}_R2.fastq.gz")

# Temporary Files
FQ1_L1_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L001_R1.fastq.gz")
FQ1_L2_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L002_R1.fastq.gz")
FQ1_L3_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L003_R1.fastq.gz")
FQ1_L4_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L004_R1.fastq.gz")
FQ2_L1_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L001_R2.fastq.gz")
FQ2_L2_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L002_R2.fastq.gz")
FQ2_L3_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L003_R2.fastq.gz")
FQ2_L4_FILE = join(FASTQ_DIR,  "{patient}-{sample}_L004_R2.fastq.gz")

# Output Files
PATHSEQ_BAM = join("output", "PathSeq", "{patient}-{sample}", "pathseq.bam")

include: "../pathogen-discovery-rules/rules/PathSeq-batch.smk"

rule all:
    input:
        expand(PATHSEQ_BAM, zip, patient=samples["patient"], sample=samples["sample"])
        # expand(config["PathSeq"]["bam_file"], zip, patient=samples["patient"], sample=samples["sample"])

rule convert_FASTQ_to_BAM:
    group:
        "FASTQ"
    input:
        fq1=FASTQ1_FILE,
        fq2=FASTQ2_FILE
    output:
        config["PathSeq"]["bam_file"]
    shell:
        "module load picard && "
        "java -Xmx8g -XX:ParallelGCThreads=5 -jar $PICARDJARPATH/picard.jar "
        "FastqToSam F1={input.fq1} F2={input.fq2} O={output} "
        "SM={wildcards.sample} RG={wildcards.sample} "
        "TMP_DIR=/lscratch/$SLURM_JOBID"

# each paired-end sample actually consists of 8 files
# {sample}-{well}_{sample_number}_{lane}_{end}_001.fastq.gz
# we need to download all four fastq files per end and combine them
rule download_FQ1_files:
    group:
        "FASTQ"
    params:
        fq = lambda wildcards: samples.loc[(wildcards.patient, wildcards.sample), "fastq_prefix"],
    output:
        L1=temp(FQ1_L1_FILE),
        L2=temp(FQ1_L2_FILE),
        L3=temp(FQ1_L3_FILE),
        L4=temp(FQ1_L4_FILE),
        fq1=FASTQ1_FILE
    shell:
        "module load google-cloud-sdk && "
        "gsutil cp {params.fq}_L001_R1_001.fastq.gz {output.L1}  && "
        "gsutil cp {params.fq}_L002_R1_001.fastq.gz {output.L2} && "
        "gsutil cp {params.fq}_L003_R1_001.fastq.gz {output.L3} && "
        "gsutil cp {params.fq}_L004_R1_001.fastq.gz {output.L4} && "
        "cat {output.L1} {output.L2} {output.L3} {output.L4} > {output.fq1}"

rule download_FQ2_files:
    group:
        "FASTQ"
    params:
        fq = lambda wildcards: samples.loc[(wildcards.patient, wildcards.sample), "fastq_prefix"],
    output:
        L1=temp(FQ2_L1_FILE),
        L2=temp(FQ2_L2_FILE),
        L3=temp(FQ2_L3_FILE),
        L4=temp(FQ2_L4_FILE),
        fq2=FASTQ2_FILE
    shell:
        "module load google-cloud-sdk && "
        "gsutil cp {params.fq}_L001_R2_001.fastq.gz {output.L1}  && "
        "gsutil cp {params.fq}_L002_R2_001.fastq.gz {output.L2} && "
        "gsutil cp {params.fq}_L003_R2_001.fastq.gz {output.L3} && "
        "gsutil cp {params.fq}_L004_R2_001.fastq.gz {output.L4} && "
        "cat {output.L1} {output.L2} {output.L3} {output.L4} > {output.fq2}"
