from os.path import join, basename
import pandas as pd
from snakemake.utils import min_version
##### set minimum snakemake version #####
min_version("5.1.2")
##### load config and sample sheets #####
configfile: join("config", "PathSeq-config.yaml")
#validate(config, schema="schemas/config.schema.yaml")

### URLs ###
Nucleatum_genome_URL = "https://ftp.ncbi.nlm.nih.gov/genomes/all/GCF/000/007/325/GCF_000007325.1_ASM732v1/GCF_000007325.1_ASM732v1_genomic.fna.gz"
Nucleatum_GTF_URL = "https://ftp.ncbi.nlm.nih.gov/genomes/all/GCF/000/007/325/GCF_000007325.1_ASM732v1/GCF_000007325.1_ASM732v1_genomic.gtf.gz"


wildcard_constraints:
    patient="SC0[0-3][0-9]",
    sample="Sample[0-9][a-c]",
    genome="Nucleatum"

patients = pd.read_csv(config["patients"], sep="\t").set_index("patient", drop=False)
# validate(samples, schema="schemas/samples.schema.yaml")

lanes = pd.read_csv(config["samples"], dtype=str, sep="\t").set_index(["patient", "sample", "lane"], drop=False)
lanes.index = lanes.index.set_levels([i.astype(str) for i in lanes.index.levels])  # enforce str in index
samples = lanes[["sample", "patient"]].drop_duplicates()
samples["genome"] = "Nucleatum"
cells = pd.read_csv(config["units"], sep="\t").set_index(["sample", "barcode", "patient"], drop=False)

# Snakemake includes
include: "../RNA-snakemake-rules/rules/cellranger.smk"
include: "../pathogen-discovery-rules/rules/PathSeq-10x.smk"
include: "../RNA-snakemake-rules/rules/SRPRISM-unpaired.smk"

# Directories
PATIENT_FASTQ_DIR = join("FASTQ", "raw", "{patient}")
CR_SAMPLE_ODIR = "{patient}-{sample}"
SRPRISM_ODIR = join("output", "SRPRISM")


# CellRanger files
CB_FASTQ_FILE = join(PATIENT_FASTQ_DIR, "{sample}_{lane}_R1_001.fastq.gz")
cDNA_FASTQ_FILE = join(PATIENT_FASTQ_DIR, "{sample}_{lane}_R2_001.fastq.gz")
CR_BAM_FILE = join(CR_SAMPLE_ODIR, "outs", "possorted_genome_bam.bam")

GENOME_FA = join("raw", "{genome}.fa")
GENOME_GTF = join("raw", "{genome}.gtf")
TRIMMED_FQ1 = join("FASTQ", "unmapped", "trimmed", "{patient}-{sample}_1.fastq.gz")
SRPRISM_UNPAIRED_INPUT_FQ = join(SRPRISM_ODIR, "{patient}", "{sample}", "unaligned_3.fq")
SRPRISM_UNPAIRED_PRIMARY_SORTED_BAM = join(SRPRISM_ODIR, "{patient}", "{sample}", "{genome}-unpaired.primary.sorted.bam")
SRPRISM_UNPAIRED_PRIMARY_SORTED_BAI = join(SRPRISM_ODIR, "{patient}", "{sample}", "{genome}-unpaired.primary.sorted.bam.bai")
SRPRISM_READ_COUNT = join(SRPRISM_ODIR, "{patient}", "{sample}", "{genome}-nreads.tsv")


rule all:
    input:
        expand(SRPRISM_READ_COUNT, zip, patient="SC028", sample="Sample6a", genome="Nucleatum"),


rule count_total_n_reads:
    input:
        SRPRISM_UNPAIRED_PRIMARY_SORTED_BAM
    output:
        SRPRISM_READ_COUNT
    shell:
        "module load samtools && "
        "samtools view -c {input} > {output}"

rule move_FQ_for_SRPRISM:
    input:
        TRIMMED_FQ1
    output:
        temp(SRPRISM_UNPAIRED_INPUT_FQ)
    shell:
        "zcat -c {input} > {output}"

### rules to download Fusobacterium Nucleatum ATCC 25688 reference files ###

rule download_Nucleatum_genome:
    wildcard_constraints:
        genome="Nucleatum"
    params:
        Nucleatum_genome_URL
    output:
        GENOME_FA
    shell:
        "wget -O - {params[0]} | gunzip -c > {output}"

rule download_Nucleatum_GTF:
    wildcard_constraints:
        genome="Nucleatum"
    params:
        Nucleatum_GTF_URL
    output:
        GENOME_GTF
    shell:
        "wget -O - {params[0]} | gunzip -c > {output}"

### rules to download FASTQ files ###

rule download_FASTQ_files:
    params:
        R1 = lambda wildcards: lanes.at[(wildcards.patient, wildcards.sample, wildcards.lane), "cell_barcode_fastq_url"],
        R2 = lambda wildcards: lanes.at[(wildcards.patient, wildcards.sample, wildcards.lane), "cDNA_fastq_url"],
    output:
        CB_FASTQ_FILE,
        cDNA_FASTQ_FILE,
    shell:
        "echo {params} && "
        "wget -O {output[0]} {params[R1]} && "
        "wget -O {output[1]} {params[R2]}"
